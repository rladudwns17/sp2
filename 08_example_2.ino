// Arduino pin assignment
#define PIN_LED  9
#define PIN_TRIG 12
#define PIN_ECHO 13

// configurable parameters
#define SND_VEL 346.0
#define INTERVAL 250
#define PULSE_DURATION 10
#define _DIST_MIN 100.0
#define _DIST_MAX 300.0

#define TIMEOUT ((INTERVAL / 2) * 1000.0)
#define SCALE (0.001 * 0.5 * SND_VEL)

unsigned long last_sampling_time;

void setup() {
  pinMode(PIN_LED, OUTPUT);
  pinMode(PIN_TRIG, OUTPUT);
  pinMode(PIN_ECHO, INPUT);
  digitalWrite(PIN_TRIG, LOW);

  Serial.begin(57600);
}

void loop() {
  float distance;
  int brightness;

  if (millis() < (last_sampling_time + INTERVAL))
    return;

  distance = USS_measure(PIN_TRIG, PIN_ECHO);

  if ((distance == 0.0) || (distance > _DIST_MAX)) {
    distance = _DIST_MAX + 10.0;
    brightness = 255;
  }
  else if (distance < _DIST_MIN) {
    distance = _DIST_MIN - 10.0;
    brightness = 255;
  }
  else if (distance <= 200.0) {
    brightness = (int)(255.0 * (200.0 - distance) / 100.0);
  }
  else {
    brightness = (int)(255.0 * (distance - 200.0) / 100.0);
  }

  analogWrite(PIN_LED, brightness);

  Serial.print("Min:");
  Serial.print(_DIST_MIN);
  Serial.print(",distance:");
  Serial.print(distance);
  Serial.print(",Max:");
  Serial.print(_DIST_MAX);
  Serial.print(",brightness:");
  Serial.println(brightness);

  last_sampling_time += INTERVAL;
}

float USS_measure(int TRIG, int ECHO)
{
  digitalWrite(TRIG, HIGH);
  delayMicroseconds(PULSE_DURATION);
  digitalWrite(TRIG, LOW);

  return pulseIn(ECHO, HIGH, TIMEOUT) * SCALE;
}
